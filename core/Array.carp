(defmodule Array

  (defn reduce [f x xs]
    (let [total x]
      (do
        (for [i 0 (count xs)]
          (set! &total (f &total (nth xs i))))
        total)))

  (defn first [a]
    @(Array.nth a 0))

  (defn last [a]
    @(Array.nth a (Int.dec (Array.count a))))

  (defn = [a b]
    (if (/= (count a) (count b))
      false
      (let-do [eq true]
        (for [i 0 (count a)]
          (when (/= @(nth a i) @(nth b i))
            (do
              (set! &eq false)
              (break))))
        eq)))

  (defn max [xs]
    (let [result (first xs)
          n (count xs)]
      (do
        (for [i 0 n]
          (let [x @(nth xs i)]
            (if (Int.< result x)
              (set! &result x)
              ())))
        result)))

  (defn min [xs]
    (let [result (first xs)
          n (count xs)]
      (do
        (for [i 0 n]
          (let [x @(nth xs i)]
            (if (Int.> result x)
              (set! &result x)
              ())))
        result)))

  (defn sum [xs]
    (Array.reduce add-ref (zero) xs))

  (defn subarray [xs start-index end-index]
    (let [result []]
      (do
        (for [i start-index end-index]
          (set! &result (push-back @&result @(nth xs i))))
        result)))

  (defn prefix-array [xs end-index]
    (subarray xs 0 end-index))

  (defn suffix-array [xs start-index]
    (subarray xs start-index (count xs)))

  (defn reverse [a]
    (let-do [i 0
             j (Int.dec (count &a))]
      (while (Int.< i j)
        (let-do [tmp @(nth &a i)]
          (aset! &a i @(nth &a j))
          (set! &i (Int.inc i))
          (aset! &a j tmp)
          (set! &j (Int.dec j))))
      a))

  (defn index-of [a e]
    (let-do [idx -1]
      (for [i 0 (count a)]
        (when (= @(nth a i) e)
          (do
            (set! &idx i)
            (break))))
      idx))

  (defn element-count [a e]
    (let-do [c 0]
      (for [i 0 (count a)]
        (when (= e @(nth a i)) (set! &c (Int.inc c))))
      c))

  (defn aupdate [a i f]
    (aset @a i (f (nth a i))))

  (defn aupdate! [a i f]
    (aset! a i (f (nth a i))))

  (defn swap [a i j]
    (let [x @(nth a i)
          y @(nth a j)]
      (aset (aset @a i y) j x)))

  (defn swap! [a i j]
    (let-do [x @(nth a i)
             y @(nth a j)]
      (aset! a i y)
      (aset! a j x)))

  ; cannot use for, because we want also be able to go downwards
  (defn range [start end step]
    (let-do [x []
             e start
             op (if (< start end) <= >=)]
      (while (op e end)
        (do
          (set! &x (push-back x e))
          (set! &e (+ e step))))
      x))

  ;(defmacro push-back! [a e]
  ;  (list 'set! a (list 'Array.push-back (list 'copy a) e)))
)
