(use IO)
(use System)
(use Int)
(use Double)
(use Array)

(load "SDL.carp")
(load "SDL_image.carp")

(Project.config "title" "Game")

(def rand-max 400)

(defn r []
  (the Int (random-between 0 rand-max)))

(defn random-lines []
  (let [p1 (SDL.point (r) (r))
        p2 (SDL.point (r) (r))
        p3 (SDL.point (r) (r))]
    [p1 p2 p3 p1]))

(deftype Images
    [img1 (Ptr SDL_Texture)
     img2 (Ptr SDL_Texture)])

(defn dimensions [texture]
  (let [w 0
        h 0]
    (do
      (SDL.query-texture texture NULL NULL (address w) (address h))
      (SDL.rect 0 0 w h))))

(def images (Images.init NULL NULL))

(defn draw [app rend]
  (let [rect (SDL.rect 32 32 (- 512 64) (- 512 64))]
    (do
      (SDL.set-render-draw-blend-mode rend SDL.blend-mode-add)
      (SDL.set-render-draw-color rend 0 0 0 255)
      (SDL.render-clear rend)
      (SDL.set-render-draw-color rend 200 250 255 255)
      (SDL.render-fill-rect rend (address rect))
      (SDL.set-render-draw-color rend 100 50 255 155)
      (let [rects [(SDL.rect 48 48 16 16)
                   (SDL.rect 48 80 16 16)
                   (SDL.rect 48 112 16 16)
                   (SDL.rect 48 144 16 16)]
            n (count &rects)]
        (SDL.render-fill-rects rend (raw rects) n))
      (SDL.set-render-draw-color rend 255 50 100 255)
      (for [x 0 512 16]
        (do
          (SDL.render-draw-line rend x 0 512 512)
          (SDL.render-draw-line rend 512 (+ 256 (/ x 2)) 0 512)))
      (SDL.set-render-draw-color rend 0 0 0 255)
      (let [lines (random-lines)
            n (count &lines)]
        (SDL.render-draw-lines rend (raw lines) n))
      (let [img @(Images.img1 &images)]
        (SDL.render-copy-ex rend
                            img
                            (address (dimensions img))
                            (address (SDL.rect 100 100 300 300))
                            (* 0.1 (from-int (SDL.get-ticks)))
                            (address (SDL.point 150 150))
                            SDL.flip-none)))))

(defn handle-events [app]
  (let [event (SDL.event-init)]
    (while (SDL.poll-event (address event))
      (let [et (SDL.event-type &event)]
        (cond (= et SDL.event-quit) (SDLApp.stop app)
              (= et SDL.event-key-down) (let [key (SDL.event-keycode &event)]
                                   (cond
                                     (= key SDL.key-escape) (SDLApp.stop app)
                                     (println "Unrecognized key.")))
              (println "Some other event happened..."))))))

(defn main []
  (let [app (SDLApp.create "~ CARP ~" 512 512 draw handle-events)
        rend @(SDLApp.renderer &app)]
    (do
      (set! images (Images.init (IMG.load-texture rend (cstr "./img/square.png"))
                                (IMG.load-texture rend (cstr "./img/carp_logo_969_no_texture.png"))))
      (SDLApp.run &app))))
